plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    ndkVersion rootProject.ext.ndkVersion
    defaultConfig {
        applicationId "io.hexhacking.xdl.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters rootProject.ext.abiFilters.split(",")
        }
        externalNativeBuild {
            cmake {
                abiFilters rootProject.ext.abiFilters.split(",")
                if (rootProject.ext.dependencyOnLocalLibrary) {
                    arguments "-DDEPENDENCY_ON_LOCAL_LIBRARY=ON"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version rootProject.ext.cmakeVersion
        }
//        ndkBuild {
//            path "src/main/cpp/Android.mk"
//        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    if (!rootProject.ext.dependencyOnLocalLibrary) {
        buildFeatures {
            prefab true
        }
    }
    packagingOptions {
        pickFirst '**/libxdl.so'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    if (rootProject.ext.dependencyOnLocalLibrary) {
        implementation project(':xdl')
    } else {
        implementation 'io.hexhacking:xdl:1.1.2'
    }
}

apply from: rootProject.file('gradle/sanitizer.gradle')
